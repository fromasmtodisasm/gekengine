<?xml version="1.0"?>
<filter mode="forward">
    <render cullmode="back" />
    
    <targets>
        <target name="Albedo" format="RGBA_UINT8"/>
        <target name="Normal" format="RG_HALF"/>
        <target name="Depth" format="R_FLOAT"/>
        <depth format="D24S8" comparison="lessequal" writemask="all" clear="1" />
    </targets>
    
    <pixel>
        <![CDATA[
            Texture2D     gs_pAlbedoMap             : register(t0);
            Texture2D     gs_pNormalMap             : register(t1);

            struct OUTPUT
            {
                float4 albedo                       : SV_TARGET0;
                float2 normal                       : SV_TARGET1;
                float  depth                        : SV_TARGET2;
            };

            float3x3 GetCoTangentFrame( float3 nNormal, float3 nPosition, float2 nTexCoord )
            {
                // get edge vectors of the pixel triangle
                float3 nPositionDX = ddx( nPosition );
                float3 nPositionDY = ddy( nPosition );
                float2 nTexCoordDX = ddx( nTexCoord );
                float2 nTexCoordDY = ddy( nTexCoord );
 
                // solve the linear system
                float3 nPerpendicularDX = cross( nNormal, nPositionDX );
                float3 nPerpendicularDY = cross( nPositionDY, nNormal );
                float3 nTangent = nPerpendicularDY * nTexCoordDX.x + nPerpendicularDX * nTexCoordDY.x;
                float3 nBiTangent = nPerpendicularDY * nTexCoordDX.y + nPerpendicularDX * nTexCoordDY.y;
 
                // construct a scale-invariant frame 
                float nReciprocal = rsqrt( max( dot(nTangent,nTangent), dot(nBiTangent,nBiTangent) ) );
                return float3x3( nTangent * nReciprocal, nBiTangent * nReciprocal, nNormal );
            }

            half2 encode (half3 n)
            {
                half2 enc = normalize(n.xy) * (sqrt(-n.z*0.5+0.5));
                enc = enc*0.5+0.5;
                return enc;
            }
            
            half3 decode (half4 enc)
            {
                half4 nn = enc*half4(2,2,0,0) + half4(-1,-1,1,-1);
                half l = dot(nn.xyz,-nn.xyw);
                nn.z = l;
                nn.xy *= sqrt(l);
                return nn.xyz * 2 + half3(0,0,-1);
            }

            OUTPUT MainPixelProgram(in INPUT kInput)
            {
                float4 nAlbedo = (gs_pAlbedoMap.Sample(gs_pLinearSampler, kInput.texcoord) * gs_nMaterialColor * kInput.color);

                [branch]
                if(nAlbedo.a < 0.5f)
                {
                    discard;
                }

                // Viewspace vertex normal
                float3 nViewNormal = (normalize(kInput.viewnormal) * (kInput.frontface ? 1 : -1));
                
                float3 nNormal;
                // Normals stored as 3Dc format, so [0,1] XY components only
                nNormal.xy = ((gs_pNormalMap.Sample(gs_pLinearSampler, kInput.texcoord).yx * 2.0) - 1.0);
                nNormal.z = sqrt(1.0 - dot(nNormal.xy, nNormal.xy));

                float3x3 nCoTangentFrame = GetCoTangentFrame(nViewNormal, -kInput.viewposition.xyz, kInput.texcoord);
                nNormal = mul(nNormal, nCoTangentFrame);

                OUTPUT kOutput;
                kOutput.albedo.xyz = nAlbedo.xyz;
                kOutput.albedo.a   = gs_bMaterialFullBright;
                kOutput.normal     = encode(nNormal);
                kOutput.depth      = kInput.viewposition.z;
                return kOutput;
            }
        ]]>
    </pixel>
</filter>
